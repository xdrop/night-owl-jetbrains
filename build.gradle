buildscript {
    ext.kotlin_version = '1.3.20'
}
plugins {
    id 'org.jetbrains.intellij' version '0.3.4'
    id 'org.jetbrains.kotlin.jvm' version '1.3.20'
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

group 'me.xdrop'
version '1.5.0'

def isDevMode = System.getProperty("dev")?.toBoolean() ?: false

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.2"
        languageVersion = "1.2"
    }
}

def pluginsIncluded = ['JavaScriptLanguage', "org.jetbrains.kotlin:$kotlin_version-release-IJ$ideaVersion-1"]
intellij {
    pluginName 'nightowl-theme'
    version "IU-2018.3.1"
    updateSinceUntilBuild true
    if (isDevMode) {
        pluginsIncluded.addAll(['izhangzhihao.rainbow.brackets:5.10',
                                'com.chrisrm.idea.MaterialThemeUI:3.5.0.1'])
    }
    plugins = pluginsIncluded
}

patchPluginXml {
    changeNotes file("$projectDir/docs/CHANGELOG.html").text
    sinceBuild '183.2940.0'
    untilBuild '191.*'
}

task colorsJar(type: Zip) {
    baseName = 'night-owl-color-scheme-only'
    extension = 'jar'

    from('src/main/resources/colors') {
        into 'colors/'
    }

    from('src/main/resources/options') {
        into 'options/'
    }

    from('colors-jar/')
}