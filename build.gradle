buildscript {
    ext.kotlin_version = '1.2.61'
}
plugins {
    id 'org.jetbrains.intellij' version '0.3.4'
    id 'org.jetbrains.kotlin.jvm' version '1.2.61'
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

group 'me.xdrop'
version '1.3.3-2018.1'

def isDevMode = System.getProperty("dev")?.toBoolean() ?: false

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.2"
        languageVersion = "1.2"
    }
}

def pluginsIncluded = ['JavaScriptLanguage', "org.jetbrains.kotlin:$kotlin_version-release-IJ$ideaVersion-1"]
intellij {
    pluginName 'nightowl-theme'
    version "IU-$ideaVersion"
    updateSinceUntilBuild true
    if (isDevMode) {
        pluginsIncluded.addAll(['izhangzhihao.rainbow.brackets:5.9.1',
                                'com.chrisrm.idea.MaterialThemeUI:2.7.1'])
    }
    plugins = pluginsIncluded
}

patchPluginXml {
    changeNotes file("$projectDir/docs/CHANGELOG.html").text
    sinceBuild '181.4203.550'
    untilBuild '182'
}

task colorsJar(type: Zip) {
    baseName = 'night-owl-color-scheme-only'
    extension = 'jar'

    from('src/main/resources/colors') {
        into 'colors/'
    }

    from('src/main/resources/options') {
        into 'options/'
    }

    from('colors-jar/')
}