buildscript {
    ext.kotlin_version = '1.4.10'
}
plugins {
    id 'org.jetbrains.intellij' version '0.7.2'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.1'
}

group 'me.xdrop'
version '1.5.8'

def isDevMode = System.getProperty("dev")?.toBoolean() ?: false

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

compileKotlin {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    kotlinOptions {
        jvmTarget = "1.8"
        apiVersion = "1.2"
        languageVersion = "1.2"
    }
}

def pluginsIncluded = ['JavaScriptLanguage', "org.jetbrains.kotlin:$kotlin_version-release-IJ2020.2-1"]
intellij {
    pluginName 'nightowl-theme'
    version "IU-2020.2"
    updateSinceUntilBuild true
    if (isDevMode) {
        pluginsIncluded.addAll(['izhangzhihao.rainbow.brackets:5.10',
                                'com.chrisrm.idea.MaterialThemeUI:5.3.2'])
    }
    plugins = pluginsIncluded
}

patchPluginXml {
    changeNotes file("$projectDir/docs/CHANGELOG.html").text
    sinceBuild '211'
    untilBuild '211.*'
}

task colorsJar(type: Zip) {
    baseName = 'night-owl-color-scheme-only'
    extension = 'jar'

    from('src/main/resources/colors') {
        into 'colors/'
    }

    from('src/main/resources/options') {
        into 'options/'
    }

    from('colors-jar/')
}
